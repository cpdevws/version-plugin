group 'org.crosslibs'
version '1.0.1'

apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    jcenter()
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

groovydoc {
    docTitle = 'Semantic Versioning Gradle Plugin<br/>(org.crosslibs.version-plugin)<br/>'
    windowTitle = 'org.crosslibs.version-plugin:1.0'
}

task docsjar(type: Jar, dependsOn: groovydoc){
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

task sourcesjar(type: Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives docsjar
    archives sourcesjar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Gradle Semantic Version Plugin'
                packaging 'jar'
                description 'Gradle plugin for versioning of build artifacts based on semantic version rules'
                url 'https://github.com/cpdevws/version-plugin'

                scm {
                    connection 'scm:git:git://github.com/cpdevws/version-plugin.git'
                    developerConnection 'scm:git:ssh://github.com:cpdevws/version-plugin.git'
                    url 'https://github.com/cpdevws/version-plugin/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'cpdevws'
                        name 'Chaitanya Prakash N'
                        email 'cpdevws@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile 'org.crosslibs:semver:1.0.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
